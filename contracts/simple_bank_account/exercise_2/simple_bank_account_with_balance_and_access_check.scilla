(******************************************************************************)
(*               Scilla version - to ensure fixed behaviour.                  *)
(******************************************************************************)
scilla_version 0

(******************************************************************************)
(*              Library code - pure, i.e., no state changes                   *)
(******************************************************************************)
library SimpleBankAccount

(* Utility function - wrap a single message into a singleton list *)
let one_msg = 
  fun (msg : Message) => 
    let nil_msg = Nil {Message} in
    Cons {Message} msg nil_msg

(* Create an Error event. *)
(* error codes: *)
(* 1 : Insufficient balance *)
(* 2 : Non-owner cannot withdraw *)
let mk_error_event =
  fun (error_code : Uint32) =>
    { _eventname : "Error"; error_code : error_code}

(******************************************************************************)
(*                        Contract definition                                 *)
(******************************************************************************)
(* owner is the address of the bank account holder. *)
(* Only the owner is allowed to withdraw. *)
(* owner is a contract parameter, so it is immutable. *)
contract SimpleBankAccount(owner : ByStr20)

(******************************************************************************)
(*                          Contract state                                    *)
(******************************************************************************)

(* field _balance : Uint128    implicitly declared *)

(******************************************************************************)
(*        Transitions - impure, i.e., state changes and input/output          *)
(******************************************************************************)

(* Deposit: Deposit money into the account *)
transition Deposit()
  (* Incoming message contains _amount field. *)
  (* Accept incoming amount *)
  accept
  (* _balance has now increased accordingly *)
end

(* Withdraw: Withdraw money from the account if the balance is sufficient. *)
(* If not, issue an error event. *)
(* Only the owner is allowed to withdraw funds from the account. *)
(* amount : amount to withdraw *)
transition Withdraw(amount : Uint128)

  (* Exercise: *)
  (* Allow the bank account to have multiple owners, who can all withdraw. *)
  (* Only the initial owner should be able to add new owners. *)

  (* Incoming message contains _sender field. *)
  sender_is_owner = builtin eq _sender owner;
  match sender_is_owner with
  | False =>
    (* Non-owner attempts withdrawal. Deny. *)
    error_code = Uint32 2;
    e = mk_error_event error_code;
    event e
  | True =>
    (* _sender is owner. Check the balance. *)
    bal <- _balance;
    insufficient_balance = builtin lt bal amount;
    match insufficient_balance with
    | True =>
      (* Not enough money *)
      error_code = Uint32 1;
      e = mk_error_event error_code;
      event e
    | False =>
      (* Enough money *)
      (* Create outgoing message carrying the desired amount *)
      msg = { _recipient : _sender; _amount : amount; _tag : ""};
      (* Wrap message in singleton list before sending *)
      msgs = one_msg msg;
      (* Send message, thus transferring money *)
      send msgs
    end
  end
end

